# 6.6.2.1 To specify the classes and the number of their instances
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dctypes: <http://purl.org/dc/dcmitype/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX void: <http://rdfs.org/ns/void#>
INSERT {
  GRAPH ?metadataGraph {
    ?distributionRdfUri void:classPartition [
        void:class ?o ;
        void:distinctSubjects ?distinctInstances
      ] .
    
  }
} WHERE { 
  GRAPH ?metadataGraph {
    ?distributionRdfUri a void:Dataset ;
      dcat:accessURL <?_inputGraph> . 
  }

  GRAPH <?_inputGraph> {
    { SELECT ?o (COUNT(DISTINCT ?s) AS ?distinctInstances) { ?s a ?o } GROUP BY ?o }
  }
}