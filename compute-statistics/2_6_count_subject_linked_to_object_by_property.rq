# 6.6.2.6 To specify the number of unique subject types that are linked to unique object types in the dataset
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dctypes: <http://purl.org/dc/dcmitype/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX void-ext: <http://ldf.fi/void-ext#>
INSERT {
  GRAPH <?_inputGraph/statistics> {
    ?distributionRdfUri void:propertyPartition [
        void:property ?p ;
        #void:triples "12419715"^^xsd:integer ;
        void:classPartition [
            void:class ?stype ;
            void:distinctSubjects ?scount ;
        ];
        void-ext:objectClassPartition [
          void:class ?otype ;
          void:distinctObjects ?ocount ;
    ]] . 

  }
} WHERE { 
  GRAPH <?_inputGraph/datasets> {
    ?distributionRdfUri a dctypes:Dataset , dcat:Distribution , void:Dataset .
  }

  GRAPH <?_inputGraph> {
    SELECT (COUNT(DISTINCT ?s) AS ?scount) ?stype ?p ?otype  (COUNT(DISTINCT ?o) AS ?ocount)  
    { 
     ?s ?p ?o . 
     ?s a ?stype .
     ?o a ?otype .
    } GROUP BY ?p ?stype ?otype
  }
}