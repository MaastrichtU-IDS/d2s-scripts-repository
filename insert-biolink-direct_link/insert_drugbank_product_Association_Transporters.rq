PREFIX x2rm: <http://ids.unimaas.nl/rdf2xml/model/>
PREFIX d2s: <http://data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bioentity: <http://bioentity.io/vocab/>
INSERT { 
    GRAPH <http://data2services/biolink/drugbank/direct_link> {   

        ### Represent transporter (Drug to protein or DNA) ChemicalToGeneAssociation
        #Should we use: ?drugUri bioentity:interacts_with ?transporterUri .

        # Direct link
        ?drugUri ?transporterActionPropertyUri ?transporterUri .

        ?transporterActionUri a bioentity:Attribute ;
            bioentity:name ?transporterAction .

        ?transporterUri a bioentity:GeneOrGeneProduct ;
            bioentity:name ?transporterName ;
            bioentity:id ?transporterId .

        # Publications
        ?transporterArticleUri a bioentity:Publication ;
            bioentity:id ?transporterArticlePubmedId ;
            bioentity:name ?transporterArticleCitation ;
            bioentity:relates_to ?transporterUri . 

    }
}
WHERE {
    SERVICE <http://localhost:7200/repositories/test>  {
        GRAPH <http://kraken/graph/xml2rdf> {

            # Get drug primary ID
            ?drugObj a x2rm:drugbank\/drug ;
                # Get drug primary ID
                x2rm:hasChild [
                    a x2rm:drugbank\/drug\/drugbank-id ;
                    x2rm:hasAttribute [ a x2rm:drugbank\/drug\/drugbank-id\/\@primary ] ;
                    x2rm:hasValue ?drugId
                ] ;
                # Get drug-transporters associations
                x2rm:hasChild [
                    a x2rm:drugbank\/drug\/transporters; 
                    x2rm:hasChild [
                        a x2rm:drugbank\/drug\/transporters\/transporter ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank\/drug\/transporters\/transporter\/id ; 
                            x2rm:hasValue ?transporterId
                        ] ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank\/drug\/transporters\/transporter\/name; 
                            x2rm:hasValue ?transporterName 
                        ] ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank\/drug\/transporters\/transporter\/organism;
                            x2rm:hasValue ?transporterOrganism 
                        ] ;
                        # Possible ACTIONS:
                        # inhibitor, activator, agonist, antagonist, binder, antibody
                        x2rm:hasChild [
                            a x2rm:drugbank\/drug\/transporters\/transporter\/actions; 
                            x2rm:hasChild [ 
                                a x2rm:drugbank\/drug\/transporters\/transporter\/actions\/action; 
                                x2rm:hasValue ?transporterAction
                            ] 
                        ] ;
                        # Get drug-transporters PUBLICATIONS.
                        x2rm:hasChild [ 
                            a x2rm:drugbank\/drug\/transporters\/transporter\/references;
                            x2rm:hasChild [
                                a x2rm:drugbank\/drug\/transporters\/transporter\/references\/articles; 
                                x2rm:hasChild [ 
                                    a x2rm:drugbank\/drug\/transporters\/transporter\/references\/articles\/article;
                                    x2rm:hasChild [ 
                                        a x2rm:drugbank\/drug\/transporters\/transporter\/references\/articles\/article\/pubmed-id; 
                                        x2rm:hasValue ?transporterArticlePubmedId 
                                    ];
                                    x2rm:hasChild [
                                        a x2rm:drugbank\/drug\/transporters\/transporter\/references\/articles\/article\/citation; 
                                        x2rm:hasValue ?transporterArticleCitation
                                    ]
                                ] 
                            ] 
                        ]
                    ]
                ] .


            BIND ( iri(concat("http://identifiers.org/drugbank/", ?drugId)) AS ?drugUri )
            BIND ( iri(concat("http://identifiers.org/drugbank/", ?transporterId)) AS ?transporterUri )
            BIND ( iri(concat("http://data2services/model/association/transporter/", md5(concat(?drugId, ?transporterId)))) AS ?transporterAssociationUri )
            BIND ( iri(concat("http://data2services/vocab/action/", ?transporterAction)) AS ?transporterActionUri )

            BIND ( iri(concat("http://identifiers.org/pubmed/", ?transporterArticlePubmedId)) AS ?transporterArticleUri ) .

            VALUES (?transporterAction ?transporterActionPropertyUri) {
                ( "inhibitor" bioentity:decreases_transport_of )
                ( "antagonist" bioentity:decreases_transport_of )
                ( "agonist" bioentity:increases_transport_of )
                ( "activator" bioentity:increases_transport_of )
                (UNDEF bioentity:affects_transport_of)
            }

        }
    }
}