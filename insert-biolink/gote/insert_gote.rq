PREFIX d2sgeneric: <http://data2services/data/emonet/ncats/preppi/preppi_final600.tsv/>
PREFIX d2smodel: <http://data2services/model/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <http://w3id.org/biolink/vocab/>
PREFIX w3idvocab: <https://w3id.org/data2services/vocab/>
INSERT
{
  GRAPH <?_outputGraph> {  
   ?protUri a bl:Protein ;
       . 

   ?tissueUri a bl:GrossAnatomicalStructure ; # tissue
      bl:name ?tissueName .

   ?pathwayUri a bl:Pathway ; # tissue-specific molecular pathway
      bl:name ?pathwayName ;
      bl:part_of ?tissueUri .

   ?interactionUri a bl:MacromolecularMachineToBiologicalProcessAssociation ;
            bl:relation bl:expressed_in ;
            bl:macromolecular_machine_to_biological_process_association_subject ?protUri ;
            bl:macromolecular_machine_to_biological_process_association_object ?pathwayUri ;
            bl:has_confidence_level ?pathwayPvalueUri ;
            bl:quantifier_qualifier ?highScoreUri ;
            bl:quantifier_qualifier ?specificScoreUri .

    ?highScoreUri a bl:RelationshipQuantifier ;
            bl:id "zScoreHighGpcrExpression" ;
            bl:name "High GPCR z-score" ;
            bl:description "z-score of GPCR represents how high the GPCR is expressed in the tissue" ;
            bl:node_property ?zScoreHighGpcrExpression .

    ?specificScoreUri a bl:SpecificityQuantifier ;
            bl:id "zScoreSpecificExpression" ;
            bl:name "Specific GPCR z-score" ;
            bl:description "z-score of GPCR represents how specific the GPCR is expressed in the tissue" ;
            bl:node_property ?zScoreSpecificExpression .

    ?pathwayPvalueUri a bl:ConfidenceLevel ;
            bl:id "pathwayPvalue" ;
            bl:name "p-value of the pathway" ;
            bl:node_property ?pathwayPvalue .

 }
}
WHERE {
  SERVICE <?_serviceUrl>  {
    GRAPH <?_inputGraph> {
      # gpcr_pathway.R: predicting G-protein independent pathways transduced by beta-arrestins.
      # gpcr_gp_pathway.R: predicting G-protein dependent pathways transduced by G-proteins.
      # do a bind on contains _gp_ to have different type?

      ?s d2smodel:Gpcruniprotid ?protId .
      ?s d2smodel:Pathwaynames ?pathwayName .
      ?s d2smodel:Tissuename ?tissueName .

      BIND( iri(concat("http://identifiers.org/uniprot/", ?protId ) ) AS ?protUri )
      BIND( iri(concat("https://w3id.org/data2services/data/pathway/", md5(?pathwayName) ) ) AS ?pathwayUri )
      BIND( iri(concat("https://w3id.org/data2services/data/tissue/", md5(?tissueName) ) ) AS ?tissueUri )
      BIND( iri(concat("https://w3id.org/data2services/data/protein/interaction/", md5(concat(?protId, ?tissueName, ?pathwayName)) ) ) AS ?interactionUri )

       OPTIONAL { 
         ?s d2smodel:Pathwaypvalue ?pathwayPvalue .
         BIND( iri(concat(str(?interactionUri), "/pathway_pvalue")) AS ?pathwayPvalueUri)
       } OPTIONAL { 
         ?s d2smodel:Zscorehighgpcrexpression ?zScoreHighGpcrExpression .
         BIND( iri(concat(str(?interactionUri), "/high_score")) AS ?highScoreUri)
       } OPTIONAL { 
         ?s d2smodel:Zscorespecificexpression ?zScoreSpecificExpression .
         BIND( iri(concat(str(?interactionUri), "/specific_score")) AS ?specificScoreUri)
       }

  # Avoid taking the first row with columns labels
  FILTER( ?tissueName != "TissueName") 
}
}
}