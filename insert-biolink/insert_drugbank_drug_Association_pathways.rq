PREFIX x2rm: <http://ids.unimaas.nl/rdf2xml/model/>
PREFIX d2s: <http://data2services/vocab/>
PREFIX bl: <http://bioentity.io/vocab/>
INSERT 
{ 
    GRAPH <http://data2services/biolink/drugbank> {   
        # Pathways (use blank node)
        ?drugPathwayAssociationUri a bl:ChemicalToPathwayAssociation ;
             bl:relation bl:affects_activity_of ;
             bl:subject ?drugUri ;
             bl:object ?drugPathwayUri .

         # Direct link
         #?drugUri bl:affects_activity_of ?drugPathwayUri .


        ?drugPathwayUri a bl:Pathway ;
            bl:id ?drugPathwaySmpdbId ;
            bl:name ?drugPathwayName .


        # TODO: add enzymes? And drugs?
    }
}
WHERE {
    SERVICE <http://localhost:7200/repositories/test>  {
        GRAPH <http://kraken/graph/xml2rdf> {

            ?drugObj a x2rm:drugbank\/drug ;
                # Get drug primary ID
                x2rm:hasChild [
                    a x2rm:drugbank\/drug\/drugbank-id ;
                    x2rm:hasAttribute [ a x2rm:drugbank\/drug\/drugbank-id\/\@primary ] ;
                    x2rm:hasValue ?drugId
                ] ;
                # Get PATHWAYS
                x2rm:hasChild [ a x2rm:drugbank\/drug\/pathways; 
                    x2rm:hasChild [ 
                        a x2rm:drugbank\/drug\/pathways\/pathway;
                        x2rm:hasChild [ 
                            a x2rm:drugbank\/drug\/pathways\/pathway\/smpdb-id; 
                            x2rm:hasValue ?drugPathwaySmpdbId 
                        ] ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank\/drug\/pathways\/pathway\/name; 
                            x2rm:hasValue ?drugPathwayName 
                        ] 
                    ]
                ] .

            BIND ( iri(concat("http://identifiers.org/drugbank/", ?drugId)) AS ?drugUri )
            BIND ( iri(concat("http://identifiers.org/smpdb/", ?drugPathwaySmpdbId) ) AS ?drugPathwayUri )
            BIND ( iri(concat("http://data2services/model/association/pathway/", md5(concat(?drugId, ?drugPathwaySmpdbId)))) AS ?drugPathwayAssociationUri )
        }
    }
}