PREFIX x2rm: <http://data2services/model/>
PREFIX d2s: <http://data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <http://w3id.org/biolink/vocab>
INSERT 
{ 
    GRAPH <http://data2services/biolink/drugbank> {  
        ?drugUri a bl:Drug ;
            bl:name ?drugName ;
            bl:id ?drugId ;
            bl:systematic_synonym ?drugAltId ;
            bl:description ?drugDescription ;
            #bl:same_as ?drugAltIdUri ;
            bl:same_as ?casNumberUri ;
            bl:same_as ?uniiUri ;
            bl:filler ?drugStateUri ;
            bl:filler ?drugGroupUri .

        # X-refs
        ?casNumberUri bl:id ?casNumber .
        ?uniiUri bl:id ?unii .

        # TODO: Make only the model/State type a subClassOf?
        ?drugStateUri a <http://data2services/model/State> ;
            rdfs:subClassOf bl:Attribute ;
            bl:id ?drugState .

        ?drugGroupUri a <http://data2services/model/Group> ;
            rdfs:subClassOf bl:Attribute ;
            bl:id ?drugGroup .

    }
}
WHERE {
    SERVICE <http://localhost:7200/repositories/test>  {
        GRAPH <http://data2services/graph/xml2rdf> {
        
            # Get drug primary ID
            ?drugObj a x2rm:drugbank\/drug ;
                # Get drug primary ID
                x2rm:hasChild [
                    a x2rm:drugbank\/drug\/drugbank-id ;
                    x2rm:hasAttribute [ a x2rm:drugbank\/drug\/drugbank-id\/\@primary ] ;
                    x2rm:hasValue ?drugId
                ] ;
                # Get Drug single informations (id, name, description...)
                x2rm:hasChild [ 
                    a x2rm:drugbank\/drug\/drugbank-id ; 
                    x2rm:hasValue ?drugAltId 
                ] ;
                x2rm:hasChild [ 
                    a x2rm:drugbank\/drug\/cas-number; 
                    x2rm:hasValue ?casNumber 
                ] ;
                x2rm:hasChild [ 
                    a x2rm:drugbank\/drug\/unii; 
                    x2rm:hasValue ?unii 
                ] ;
                x2rm:hasChild [ 
                    a x2rm:drugbank\/drug\/name; 
                    x2rm:hasValue ?drugName
                ] ;
                x2rm:hasChild [ 
                    a x2rm:drugbank\/drug\/description; 
                    x2rm:hasValue ?drugDescription 
                ] ;
                x2rm:hasChild [ 
                    a x2rm:drugbank\/drug\/state; 
                    x2rm:hasValue ?drugState 
                ] ;
                x2rm:hasChild [ 
                    a x2rm:drugbank\/drug\/groups; 
                    x2rm:hasChild [
                        a x2rm:drugbank\/drug\/groups\/group; 
                        x2rm:hasValue ?drugGroup
                    ]
                ] .

            # TODO: synthesis-reference = Patent how it is synthesis
            # TODO: a lot of "text description" have not been done. Trouble to get where they should be defined... https://biolink.github.io/biolink-model/docs/Drug.html
            # indication, pharmacodynamics, mechanism-of-action, toxicity, metabolism, absorption, half-life, protein-binding, route-of-elimination, volume-of-distribution
            # clearance

            # TODO: atc-codes, dosages, patents, sequences

            BIND ( iri(concat("http://identifiers.org/drugbank/", ?drugId)) AS ?drugUri )
            # Not really drugbank ID: BIND ( iri(concat("http://identifiers.org/drugbank/", ?drugAltId)) AS ?drugAltIdUri )
            BIND ( iri(concat("http://identifiers.org/cas/", ?casNumber)) AS ?casNumberUri )
            BIND ( iri(concat("http://identifiers.org/unii/", ?unii)) AS ?uniiUri )

            BIND ( iri(concat("http://data2services/model/state/", ?drugState)) AS ?drugStateUri )
            BIND ( iri(concat("http://data2services/model/group/", ?drugGroup)) AS ?drugGroupUri )
        }
    }
}