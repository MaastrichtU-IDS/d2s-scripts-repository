PREFIX kraken: <http://data2services/data/hgnc/hgnc_complete_set.tsv/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <http://w3id.org/biolink/vocab>
PREFIX d2svocab: <http://data2services/vocab/>
INSERT
{
  GRAPH <http://data2services/biolink/hgnc> {  
   ?hgncUri a bl:Gene. 
   ?hgncUri bl:iri ?hgncUri .
   ?hgncUri bl:id ?Hgnc_id .
   ?hgncUri bl:Name ?Name .
   ?hgncUri rdfs:label ?Name .
   ?hgncUri bl:systematic_synonym ?Symbol .
   ?hgncUri bl:systematic_synonym ?singleAlias_symbol .

   ?hgncUri d2svocab:Status ?Status .
   # Use bl:ConfidenceLevel (Level of confidence in a statement)? Values: "Approved", "Symbol Withdrawn", "Entry Withdrawn"

   ?hgncUri bl:category ?Locus_type .
   # Could it be bl:category? Values: "RNA, long non-coding", "gene with protein product", "pseudogene", "virus integration site"
   ?hgncUri bl:category ?Locus_group .
   # Almost same as Locus_type. Values: "non-coding RNA", "protein-coding gene", "pseudogene", "other"

   #?hgncUri bl:GenomicSequenceLocalization ?
   # Should we use https://biolink.github.io/biolink-model/docs/located_in.html ?

   ?hgncUri bl:update_date ?DateModified .


   # https://www.ncbi.nlm.nih.gov/gene/1047
   ?hgncUri bl:same_as ?EntrezIdUri .

   # http://identifiers.org/ensembl/ENSG00000153132
   ?hgncUri bl:same_as ?EnsemblGeneUri .

   # http://identifiers.org/mim/601858
   ?hgncUri bl:same_as ?OmimIdUri .

   # https://www.ncbi.nlm.nih.gov/nuccore/D86322
   ?hgncUri bl:same_as ?AccessionNumbersUri .

   # https://biolink.github.io/biolink-model/docs/TranscriptToGeneRelationship.html
   ?refseqAssociationUri a bl:TranscriptToGeneRelationship ;
            bl:transcript_to_gene_relationship_subject ?RefseqAccessionUri ;
            bl:transcript_to_gene_relationship_object ?hgncUri ;
            bl:relation bl:expresses .

   ?RefseqAccessionUri a bl:Transcript . # https://www.ncbi.nlm.nih.gov/nuccore/NM_004362

   # https://biolink.github.io/biolink-model/docs/has_gene_product.html
   ?hgncUri bl:has_gene_product ?UniprotIdsUri .

   ?hgncUri bl:same_as ?EnsemblIdSuppliedByEnsemblUri .

   # https://biolink.github.io/biolink-model/docs/GeneToGeneHomologyAssociation.html
   ?mgiAssociationUri a bl:GeneToGeneHomologyAssociation ;
            bl:relation bl:homologous_to ;
            bl:gene_to_gene_association_subject ?hgncUri ;
            bl:gene_to_gene_association_object ?MouseGenomeDatabaseIdUri .

   ?suppliedMgiAssociationUri a bl:GeneToGeneHomologyAssociation ;
            bl:relation bl:homologous_to ;
            bl:gene_to_gene_association_subject ?hgncUri ;
            bl:gene_to_gene_association_object ?MouseGenomeDatabaseIdSuppliedByMgiUri .

   ?rgdAssociationUri a bl:GeneToGeneHomologyAssociation ;
            bl:relation bl:homologous_to ;
            bl:gene_to_gene_association_subject ?hgncUri ;
            bl:gene_to_gene_association_object ?RgdIdUri .



   ?hgncUri bl:located_in ?GenomicSequenceLocalizationUri .
   ?hgncUri bl:located_in ?CcdsIdUri . # TODO: atm it generates http://identifiers.org/ccds/CCDS7241 but this does not resolve. It would need to be http://identifiers.org/ccds/CCDS7241.1

   ?GenomicSequenceLocalizationUri a bl:GenomicSequenceLocalization ;
      bl:genomic_sequence_localization_subject ?hgncUri ;
      bl:genomic_sequence_localization_object ?LocationUri .

    ?LocationUri a bl:GenomicEntity ;
      bl:id ?Location ;
      bl:Name ?Location ;
      bl:category "chromosome" .



   # Publication
   ?hgncUri bl:publications ?PubmedIdUri .
   ?PubmedIdUri a bl:Publication ;
      bl:id ?Pubmed_id ;
      bl:iri ?PubmedIdUri ;
      bl:relates_to ?hgncUri .

    ?hgncUri bl:filler ?GeneFamilyUri .
    ?GeneFamilyUri a <http://data2services/model/GeneFamily> ;
      bl:subClassOf bl:Attribute ;
      bl:name ?Gene_family .

    # Use bl:ConfidenceLevel? (Level of confidence in a statement) Values: "Approved", "Symbol Withdrawn", "Entry Withdrawn"
    ?hgncUri bl:filler ?StatusUri .
    ?StatusUri a <http://data2services/model/Status> ;
          rdfs:subClassOf bl:Attribute ;
          bl:Name ?Status .


 }
}
WHERE {
  SERVICE <http://localhost:7200/repositories/test>  {
    GRAPH <http://data2services/graph/autor2rml> {
      ?s kraken:Hgnc_id ?Hgnc_id .
       OPTIONAL { 
         ?s kraken:Name ?Name .
       
       } OPTIONAL {
         ?s kraken:Symbol ?Symbol .
       
       } OPTIONAL { 
         ?s kraken:Status ?Status .
         BIND ( iri(concat("http://data2services/model/status/", REPLACE(?Status, " ", "", "i")) ) AS ?StatusUri )
       
       } OPTIONAL { 
         ?s kraken:Locus_type ?Locus_type .
       
       } OPTIONAL { 
         ?s kraken:Locus_group ?Locus_group .
       
       } OPTIONAL {
         ?s kraken:Alias_symbol ?Alias_symbol . # array
         BIND( if( contains(?Alias_symbol, "|"),
              strbefore(?Alias_symbol, "|") ,
              ?Alias_symbol )
         AS ?singleAlias_symbol )
       
       } OPTIONAL {
         ?s kraken:Alias_name ?Alias_name . # array
         BIND( if( contains(?Alias_name, "|"),
              strbefore(?Alias_name, "|") ,
              ?Alias_name )
         AS ?singleAlias_name )

       } OPTIONAL {
         ?s kraken:Prev_symbol ?Prev_symbol . # array
         BIND( if( contains(?Prev_symbol, "|"),
              strbefore(?Prev_symbol, "|") ,
              ?Prev_symbol )
         AS ?singlePrev_symbol )
       
       } OPTIONAL {
         ?s kraken:Prev_name ?Prev_name . # array

       } OPTIONAL {
         ?s kraken:Gene_family ?Gene_family . # array
         BIND( if( contains(?Gene_family, "|"),
              iri(concat("https://data2services/model/gene-family/", md5(strbefore(?Gene_family, "|")) ) ) ,
              iri(concat("https://data2services/model/gene-family/", md5(?Gene_family) ) ) ) 
         AS ?GeneFamilyUri ) # TODO: not good for URI

       } OPTIONAL {
         ?s kraken:Gene_family_id ?Gene_family_id . # array
         # TODO: Generate URI fropm this one instead of using md5?!
       
       } OPTIONAL {
         ?s kraken:Synonyms ?Synonyms .
       
       } OPTIONAL {
         ?s kraken:NameSynonyms ?NameSynonyms .
       
       } OPTIONAL {
         # TODO: also do Location_sortable? WHY this if statement???
         ?s kraken:Location ?Location .
         BIND( if( contains(?Location, " "),
              iri("") ,
              iri(concat("http://data2services/model/sequence-localization/", md5(concat(?Hgnc_id, ?Location))))
              ) 
         AS ?GenomicSequenceLocalizationUri )

         BIND( if( contains(?Location, " "),
              iri("") ,
              iri(concat("http://data2services/model/chromosome/", ?Location) )
              ) 
         AS ?LocationUri )

         #FILTER NOT EXISTS { contains(?Location, " ")  }

         #BIND( iri(concat("http://data2services/model/sequence-localization/", md5(concat(?Hgnc_id, ?Location)))) AS ?GenomicSequenceLocalizationUri )

         #BIND( iri(concat("http://data2services/model/chromosome/", ?Location) ) AS ?LocationUri )
       
       } OPTIONAL {
         ?s kraken:Date_approved_reserved ?Date_approved_reserved . # Format: 2009-07-20

       } OPTIONAL {
         ?s kraken:Date_symbol_changed ?Date_symbol_changed . # Format: 2009-07-20

       } OPTIONAL {
         ?s kraken:Date_name_changed ?Date_name_changed . # Format: 2009-07-20

       } OPTIONAL {
         ?s kraken:Date_modified ?Date_modified . # Format: 2009-07-20
       
       } OPTIONAL {
         ?s kraken:Entrez_id ?Entrez_id .
         BIND( iri(concat("https://www.ncbi.nlm.nih.gov/gene/", ?Entrez_id) ) AS ?EntrezIdUri )

       } OPTIONAL {
         ?s kraken:Ensembl_gene_id ?Ensembl_gene_id .
         BIND( iri(concat("http://identifiers.org/ensembl/", ?Ensembl_gene_id) ) AS ?EnsemblGeneIdUri )

       } OPTIONAL {
         ?s kraken:Vega_id ?Vega_id .

       } OPTIONAL {
         ?s kraken:Ucsc_id ?Ucsc_id .

       } OPTIONAL { 
         ?s kraken:Ena ?Ena .
         BIND( if( contains(?Ena, "|"),
              strbefore(?Ena, "|") ,
              ?Ena )
         AS ?singleEna )

       } OPTIONAL {
         ?s kraken:Refseq_accession ?Refseq_accession .
         BIND( if( contains(?Refseq_accession, "|"),
              iri(concat("https://www.ncbi.nlm.nih.gov/nuccore/", strbefore(?Refseq_accession, "|") ) ) ,
              iri(concat("https://www.ncbi.nlm.nih.gov/nuccore/", ?Refseq_accession ) ) ) 
         AS ?RefseqAccessionUri )
         BIND ( iri(concat("http://data2services/model/association/transcript/", md5(concat(?Hgnc_id, ?Refseq_accession)))) AS ?refseqAssociationUri )

       } OPTIONAL {
         ?s kraken:Ccds_id ?Ccds_id .

         BIND( if( contains(?Ccds_id, "|"),
                iri(concat("http://identifiers.org/ccds/", strbefore(?Ccds_id, "|") ) ) ,
                iri(concat("http://identifiers.org/ccds/", ?Ccds_id ) ) )
         AS ?CcdsIdUri )

       } OPTIONAL {
         ?s kraken:Uniprot_ids ?Uniprot_ids .
         # If there is a comma we take only the first iteration.
         # TODO: also take the STRAFTER?
         BIND( if( contains(?Uniprot_ids, "|"),
              iri(concat("http://identifiers.org/uniprot/", strbefore(?Uniprot_ids, "|") ) ) ,
              iri(concat("http://identifiers.org/uniprot/", ?Uniprot_ids ) ) ) 
         AS ?UniprotIdsUri )


       } OPTIONAL {
         ?s kraken:Pubmed_id ?Pubmed_id .
         BIND( if( contains(?Pubmed_id, "|"),
              iri(concat("http://identifiers.org/pubmed/", strbefore(?Pubmed_id, "|") ) ) ,
              iri(concat("http://identifiers.org/pubmed/", ?Pubmed_id ) ) ) 
         AS ?PubmedIdUri )

       } OPTIONAL {
         ?s kraken:Mgd_id ?Mgd_id .
         BIND( if( contains(?Mgd_id, "|"),
              iri(concat("http://identifiers.org/", strbefore(?Mgd_id, "|") ) ) ,
              iri(concat("http://identifiers.org/", ?Mgd_id ) ) ) 
         AS ?MouseGenomeDatabaseIdUri )
         BIND ( iri(concat("http://data2services/model/association/homology/", md5(concat(?Hgnc_id, ?Mgd_id)))) AS ?mgiAssociationUri )

       } OPTIONAL {
         ?s kraken:Rgd_id ?Rgd_id .
         BIND( if( contains(?Rgd_id, "|"),
              iri(concat("http://identifiers.org/", strbefore(?Rgd_id, "|") ) ) ,
              iri(concat("http://identifiers.org/", ?Rgd_id ) ) ) 
         AS ?RgdIdUri )
         BIND ( iri(concat("http://data2services/model/association/homology/", md5(concat(?Hgnc_id, ?Rgd_id)))) AS ?rgdAssociationUri )

       } OPTIONAL {
         ?s kraken:Lsdb ?Lsdb .
         BIND( if( contains(?Lsdb, "|"),
              iri(concat("http://identifiers.org/", strafter(?Lsdb, "|") ) ) ,
              iri(concat("http://identifiers.org/", ?Lsdb ) ) ) 
         AS ?LsdbUri ) # TODO: LRG_359|http://ftp.ebi.ac.uk/pub/databases/lrgex/LRG_359.xml

       
       } OPTIONAL {
         ?s kraken:Cosmic ?Cosmic .

       } OPTIONAL {
         ?s kraken:Omim_id ?Omim_id .
         # TODO: also take the STRAFTER?
         BIND( if( contains(?Omim_id, "|"),
              iri(concat("http://identifiers.org/mim/", strbefore(?Omim_id, "|") ) ) ,
              iri(concat("http://identifiers.org/mim/", ?Omim_id ) ) ) 
         AS ?OmimIdUri )

       } OPTIONAL {
         ?s kraken:Mirbase ?Mirbase .
         # TODO: identifiers.org/mirbase:MI0000651
         BIND( iri(concat("http://identifiers.org/mirbase/", ?Mirbase ) ) 
         AS ?MirbaseUri )

       } OPTIONAL {
         ?s kraken:Homeodb ?Homeodb .
         # TODO: 8553

       } OPTIONAL {
         ?s kraken:Snornabase ?Snornabase .
         # Not really interesting DB https://www-snorna.biotoul.fr/plus.php?id=U8 (SR0000002)

       } OPTIONAL {
         ?s kraken:Bioparadigms_slc ?Bioparadigms_slc . # TODO: SLC49A1
       
       } OPTIONAL {
         ?s kraken:Orphanet ?Orphanet . # TODO: 246647

       } OPTIONAL {
         ?s kraken:PseudogeneOrg ?PseudogeneOrg . # TODO: PGOHUM00000291221

       } OPTIONAL {
         ?s kraken:Horde_id ?Horde_id . # TODO: OR1A1

       } OPTIONAL {
         ?s kraken:Merops ?Merops . # TODO: I43.950

       } OPTIONAL {
         ?s kraken:Imgt ?Imgt . # TODO: IGHA2

       } OPTIONAL {
         ?s kraken:Iuphar ?Iuphar . # TODO: objectId:763

       } OPTIONAL {
         ?s kraken:Kznf_gene_catalog ?Kznf_gene_catalog . # TODO: 1226

       } OPTIONAL {
         ?s kraken:MamitTrnadb ?MamitTrnadb . # TODO: 13

       } OPTIONAL {
         ?s kraken:Cd ?Cd . # TODO: CD243


       } OPTIONAL {
         ?s kraken:Lncrnadb ?Lncrnadb . # TODO: AFAP1AS

       } OPTIONAL {
         ?s kraken:Enzyme_id ?Enzyme_id . # array TODO: 6.2.1.16
         BIND( if( contains(?Enzyme_id, "|"),
              iri(concat("http://identifiers.org/ec-code/", strbefore(?Enzyme_id, "|") ) ) ,
              iri(concat("http://identifiers.org/ec-code/", ?Enzyme_id ) ) ) 
         AS ?EnzymeIdsUri )

       } OPTIONAL {
         ?s kraken:Intermediate_filament_db ?Intermediate_filament_db . # TODO: HGNC:1040

       } OPTIONAL {
         ?s kraken:Rna_central_ids ?Rna_central_ids . # TODO: URS00007E4F6E
         BIND( iri(concat("https://rnacentral.org/rna/", ?Rna_central_ids) ) AS ?RnaCentralIdsUri )
       }

  BIND ( iri(concat("http://identifiers.org/", ?Hgnc_id)) AS ?hgncUri )

  # Avoid taking the first row with columns labels
  #FILTER( !contains(?Hgnc_id, "hgnc_id") )
  FILTER( ?Hgnc_id != "hgnc_id") 
}
}
}