PREFIX x2rm: <http://ids.unimaas.nl/rdf2xml/model/>
PREFIX d2s: <http://data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bioentity: <http://bioentity.io/vocab/>
PREFIX sio: <http://semanticscience.org/resource/>
INSERT { 
    GRAPH <http://data2services/sio/drugbank> {   

        ### Represent target (Drug to protein or DNA) ChemicalToGeneAssociation
        #Should we use: ?drugUri bioentity:interacts_with ?targetUri .
        ?targetAssociationUri a sio:association\/chemical-protein ;
            sio:SIO_000115 ?drugUri ; # refers to
            sio:SIO_000115 ?targetUri ;
            sio:SIO_000772 ?targetArticleUri ; #  has evidence
            sio:SIO_001403 ?targetOrganism ; #  is associated with  
            sio:SIO_000008 ?targetActionPropertyUri . # has attribute: normality increased. 
            # TODO: fix it. Should be better expressed

        #?targetOrganism a sio:SIO_010000 . # organism

        ?targetUri a sio:SIO_010043 ; # protein
            rdfs:label ?targetName ;
            sio:SIO_000115 ?targetId .

        # Publications
        ?targetArticleUri a sio:SIO_001029 ;    # peer reviewed article
            sio:SIO_000115 ?targetArticlePubmedId ; # identifier
            rdfs:label ?targetArticleCitation . 

    }
}
WHERE {
    SERVICE <http://localhost:7200/repositories/test>  {
        GRAPH <http://kraken/graph/xml2rdf> {

            # Get drug primary ID
            ?drugObj a x2rm:drugbank\/drug ;
                # Get drug primary ID
                x2rm:hasChild [
                    a x2rm:drugbank\/drug\/drugbank-id ;
                    x2rm:hasAttribute [ a x2rm:drugbank\/drug\/drugbank-id\/\@primary ] ;
                    x2rm:hasValue ?drugId
                ] ;
                # Get drug-TARGETS associations
                x2rm:hasChild [
                    a x2rm:drugbank\/drug\/targets; 
                    x2rm:hasChild [
                        a x2rm:drugbank\/drug\/targets\/target ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank\/drug\/targets\/target\/id ; 
                            x2rm:hasValue ?targetId
                        ] ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank\/drug\/targets\/target\/name; 
                            x2rm:hasValue ?targetName 
                        ] ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank\/drug\/targets\/target\/organism;
                            x2rm:hasValue ?targetOrganism 
                        ] ;
                        # Possible ACTIONS:
                        # inhibitor, activator, agonist, antagonist, binder, antibody
                        x2rm:hasChild [
                            a x2rm:drugbank\/drug\/targets\/target\/actions; 
                            x2rm:hasChild [ 
                                a x2rm:drugbank\/drug\/targets\/target\/actions\/action; 
                                x2rm:hasValue ?targetAction
                            ] 
                        ] ;
                        # Get drug-targets PUBLICATIONS.
                        x2rm:hasChild [ 
                            a x2rm:drugbank\/drug\/targets\/target\/references;
                            x2rm:hasChild [
                                a x2rm:drugbank\/drug\/targets\/target\/references\/articles; 
                                x2rm:hasChild [ 
                                    a x2rm:drugbank\/drug\/targets\/target\/references\/articles\/article;
                                    x2rm:hasChild [ 
                                        a x2rm:drugbank\/drug\/targets\/target\/references\/articles\/article\/pubmed-id; 
                                        x2rm:hasValue ?targetArticlePubmedId 
                                    ];
                                    x2rm:hasChild [
                                        a x2rm:drugbank\/drug\/targets\/target\/references\/articles\/article\/citation; 
                                        x2rm:hasValue ?targetArticleCitation
                                    ]
                                ] 
                            ] 
                        ]
                    ]
                ] .


            BIND ( iri(concat("http://identifiers.org/drugbank/", ?drugId)) AS ?drugUri )
            BIND ( iri(concat("http://identifiers.org/drugbank/", ?targetId)) AS ?targetUri )
            BIND ( iri(concat("http://data2services/model/association/target/", md5(concat(?drugId, ?targetId)))) AS ?targetAssociationUri )

            BIND ( iri(concat("http://identifiers.org/pubmed/", ?targetArticlePubmedId)) AS ?targetArticleUri ) .

            VALUES (?targetAction ?targetActionPropertyUri) {
                ( "inhibitor" sio:SIO_000751 )  # normality decreased or SIO_000499 to decrease the rate of formation
                ( "antagonist" sio:SIO_000751 )
                ( "agonist" sio:SIO_000640 )    # normality increased 
                ( "activator" sio:SIO_000640 )
                (UNDEF sio:SIO_000555)             # abnormal
            }
        }
    }
}