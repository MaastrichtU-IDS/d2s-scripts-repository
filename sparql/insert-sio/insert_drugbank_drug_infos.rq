PREFIX x2rm: <http://ids.unimaas.nl/xml2rdf/model#>
PREFIX x2rd: <http://ids.unimaas.nl/xml2rdf/data/>
PREFIX d2s: <http://data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bioentity: <http://bioentity.io/vocab/>
INSERT 
{ 
    GRAPH <?_drugbankOutputGraph> {  


        
        ?drugUri a bioentity:Drug ;
            bioentity:name ?drugName ;
            bioentity:id ?drugId ;
            bioentity:systematic_synonym ?drugAltId ;
            bioentity:description ?drugDescription ;
            #bioentity:same_as ?drugAltIdUri ;
            bioentity:same_as ?casNumberUri ;
            bioentity:same_as ?uniiUri ;
            # TODO: use SIO cross ref https://bioportal.bioontology.org/ontologies/SIO/?p=classes&conceptid=http%3A%2F%2Fsemanticscience.org%2Fresource%2FSIO_001172
            bioentity:filler ?drugStateUri ;
            bioentity:filler ?drugGroupUri .

        # X-refs
        ?casNumberUri bioentity:id ?casNumber .
        ?uniiUri bioentity:id ?unii .

        ?drugStateUri a <http://data2services/model/State> ;
            rdfs:subClassOf bioentity:Attribute ;
            bioentity:id ?drugState .

        ?drugGroupUri a <http://data2services/model/Group> ;
            rdfs:subClassOf bioentity:Attribute ;
            bioentity:id ?drugGroup .

    }
}
WHERE {
    SERVICE <?_service>  {
        GRAPH <?_drugbankInputGraph> {
        
            # Get drug primary ID
            ?drugObj a x2rm:drugbank.drug ;
                # Get drug primary ID
                x2rm:hasChild [
                    a x2rm:drugbank.drug.drugbank-id ;
                    x2rm:hasAttribute [ a x2rm:drugbank.drug.drugbank-id.-primary ] ;
                    x2rm:hasValue ?drugId
                ] ;
                # Get Drug single informations (id, name, description...)
                x2rm:hasChild [ 
                    a x2rm:drugbank.drug.drugbank-id ; 
                    x2rm:hasValue ?drugAltId 
                ] ;
                x2rm:hasChild [ 
                    a x2rm:drugbank.drug.cas-number; 
                    x2rm:hasValue ?casNumber 
                ] ;
                x2rm:hasChild [ 
                    a x2rm:drugbank.drug.unii; 
                    x2rm:hasValue ?unii 
                ] ;
                x2rm:hasChild [ 
                    a x2rm:drugbank.drug.name; 
                    x2rm:hasValue ?drugName
                ] ;
                x2rm:hasChild [ 
                    a x2rm:drugbank.drug.description; 
                    x2rm:hasValue ?drugDescription 
                ] ;
                x2rm:hasChild [ 
                    a x2rm:drugbank.drug.state; 
                    x2rm:hasValue ?drugState 
                ] ;
                x2rm:hasChild [ 
                    a x2rm:drugbank.drug.groups; 
                    x2rm:hasChild [
                        a x2rm:drugbank.drug.groups.group; 
                        x2rm:hasValue ?drugGroup
                    ]
                ] .

            # TODO: synthesis-reference = Patent how it is synthesis
            # TODO: a lot of "text description" have not been done. Trouble to get where they should be defined... https://biolink.github.io/biolink-model/docs/Drug.html
            # indication, pharmacodynamics, mechanism-of-action, toxicity, metabolism, absorption, half-life, protein-binding, route-of-elimination, volume-of-distribution
            # clearance

            # TODO: atc-codes, dosages, patents, sequences

            BIND ( iri(concat("http://identifiers.org/drugbank:">, ?drugId)) AS ?drugUri )
            # Not really drugbank ID: BIND ( iri(concat("http://identifiers.org/drugbank:">, ?drugAltId)) AS ?drugAltIdUri )
            BIND ( iri(concat("http://identifiers.org/cas/", ?casNumber)) AS ?casNumberUri )
            BIND ( iri(concat("http://identifiers.org/unii/", ?unii)) AS ?uniiUri )

            BIND ( iri(concat("http://data2services/model/state/", ?drugState)) AS ?drugStateUri )
            BIND ( iri(concat("http://data2services/model/group/", ?drugGroup)) AS ?drugGroupUri )
        }
    }
}